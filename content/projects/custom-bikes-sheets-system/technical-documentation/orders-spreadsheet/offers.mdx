---
title: Offers
description: This is a bit about the Offers 
---



The `OFFERS` sheet is a critical component of the Custom Bikes system, meticulously designed to manage the status and make adjustments to customer offers. Projects remain in this stage until the customer makes the first or full payment, at which point they move to subsequent sheets and enter production. During this stage, it is essential to ensure that each project is correctly priced and that a PDF offer document is generated before progressing. The `OFFERS` sheet plays a vital role in accurately pricing and efficiently managing all customer offers.

Using a combination of unique identifiers, dynamic pricing formulas, and real-time data imports, the `OFFERS` sheet ensures accuracy, efficiency, and profitability in creating orders. It generates both the `System Price` and `Offer Price` and integrates seamlessly with the `INVENTORY` system. Dynamic dropdowns, generated from complex formulas, display project configurations and highlight problem items, further enhancing its functionality. Its organised layout and visual design streamline the offer creation process, allowing the team to focus on delivering quality and timely customer service, making it an indispensable component of the Custom Bikes system.

---

### UI and Visual Design

The **OFFERS** sheet is designed with a clean, organised layout to facilitate easy management of offers. Key visual elements include:

- **Header Colours**: The sheet features distinct header colours to differentiate sections. The main project details, such as IDs, key dates, customer name and contact information, configuration, and frame for the bike, are under the main header in grey. The configuration data (parts and accessories) is under a red header, while the "Design / Paintwork" section is highlighted in blue. This section contains essential data like the frame, fork, and every part and accessory. This grouped section is typically kept minimised to prevent the sheet from appearing too overwhelming.
- **Conditional Formatting**: Problem items in the **Problem Items** column are highlighted in red to immediately catch the user's attention.
- **Dropdown Menus**: Used extensively for statuses, currencies, VAT percentages, configurations, and other selections to maintain data consistency and accuracy.

---

### Column Headers and Explanation

| **Column Header**                | **Explanation**                                                                                                                                                                                                                                                                                                 |
| -------------------------------- | --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------- |
| **STATUS**                       | A dropdown containing the current status of the offer (e.g., NEW, WAITING..., OFFER DOC, PAID, REMOVE). NEW, WAITING..., OFFER DOC, and PAID track the offer stages. Selecting PAID triggers an alert to input the paid amount. Selecting REMOVE archives the project and removes it from the **OFFERS** sheet. |
| **CURRENCY**                     | A dropdown containing the currency (PLN, EUR) used in the offer. Critical for generating the system price and using the Google Finance formula for exchange rates.                                                                                                                                              |
| **VAT**                          | Dropdown containing the value-added tax percentage (e.g., options include: 0%, 19%, 23%). Custom values can also be entered.                                                                                                                                                                                    |
| **SYSTEM PRICE INC. VAT**        | The net price of the components, paintwork, estimates, and margin in PLN of the bike including VAT as calculated by the system. Later converted to EUR if selected. The System Price acts as the base price for the **Offer Price**.                                                                            |
| **OFFER PRICE INC. VAT**         | The price offered to the customer, including VAT and converted to EUR if selected as a currency. This price is typically rounded to a more attractive figure.                                                                                                                                                   |
| **SYSTEM ID**                    | Unique identifier for the project within the system, formatted as `[First 5 letters of surname/project name]-[First initial]-[Random 5-digit number]`.                                                                                                                                                          |
| **ID**                           | A sequential numerical identifier for the project, starting from the maximum existing number incremented by one.                                                                                                                                                                                                |
| **SURNAME / PROJECT**            | Customer's surname or project name. After the offer document is generated, this value turns into a hyperlink linking to its corresponding offer document.                                                                                                                                                       |
| **OWNER**                        | Sales representative handling the offer (e.g., Marek, Anna, Sandra).                                                                                                                                                                                                                                            |
| **CONFIGURATION**                | A dropdown containing the type of bike, drivetrain (e.g., Chain drive (Single Speed)), and a detailed list of all the components.                                                                                                                                                                               |
| **FRAME**                        | Specific frame type and size (e.g., City Man Frame 50 cm (S)).                                                                                                                                                                                                                                                  |
| **DESIGN / PAINTWORK**           | Custom paintwork details (e.g., Orange).                                                                                                                                                                                                                                                                        |
| **DESIGN / PAINTING COST (PLN)** | Cost of the custom design or paintwork.                                                                                                                                                                                                                                                                         |
| **EST. COST OF CONFIG (PLN)**    | Estimated cost of the bike configuration, calculated from a custom formula summing the costs of all parts and accessories.                                                                                                                                                                                      |
| **PROBLEM ITEMS**                | Any items not recognised by the system and requiring special attention. Highlighted in red for easy identification.                                                                                                                                                                                             |

---

### Sample Data Explanation

**Example: Thomas Richard's Offer:**

| **Column Header**                | **Example Data**           |
| -------------------------------- | -------------------------- |
| **STATUS**                       | OFFER DOC                  |
| **CURRENCY**                     | PLN                        |
| **VAT**                          | 23%                        |
| **SYSTEM PRICE INC. VAT**        | 8,636                      |
| **OFFER PRICE INC. VAT**         | 8,799                      |
| **SYSTEM ID**                    | THOM-R-66163               |
| **ID**                           | 0915                       |
| **SURNAME / PROJECT**            | Thomas                     |
| **OWNER**                        | Sandra                     |
| **CONFIGURATION**                | Chain drive (Single Speed) |
| **FRAME**                        | City Man Frame 50 cm (S)   |
| **DESIGN / PAINTWORK**           | Orange                     |
| **DESIGN / PAINTING COST (PLN)** | 100 zł                     |
| **EST. COST OF CONFIG (PLN)**    | 5,371 zł                   |
| **PROBLEM ITEMS**                | 1 item requiring attention |

---

### Key Features and Formulas

---

### Understanding System ID and ID

In the Custom Bikes system, two identifiers are used to uniquely distinguish each project: the **System ID** and the **ID**. Both serve specific purposes and are generated differently, playing complementary roles to maintain order and clarity within project management and manufacturing processes. These are generated during offer creation.

#### System ID

- **Structure:**
  - **Format:** `[First 5 letters of surname/project name]-[First initial]-[Random 5-digit number]`
  - **Example:** `THOM-R-66163` (Richard Thomas), `ANDE-J-38521` (Jessica Anderson)
- **Generation:**
  - Created upon offer creation using the customer's surname (or project name) and the first letter of the first name.
  - A 5-digit random number is appended to ensure uniqueness.
- **Purpose:**
  - **Readability:** Designed to be human-readable and easily recognisable.
  - **System Use:** Intuitive for internal use, helping quickly identify the project/customer.
  - **Uniqueness:** The combination of letters and a random number reduces duplication risk.

The **System ID** is crafted for readability and ease of use within digital interfaces, ensuring projects are easily identifiable within the system. This ID is particularly useful for system interfaces, documentation, and internal communications.

#### ID

- **Structure:**
  - **Format:** A sequential numerical sequence.
  - **Example:** `0915`, `0973`
- **Generation:**
  - Created by finding the highest current numerical value in the system, then incrementing it by one.
  - Ensures a unique and chronologically ordered identifier.
- **Purpose:**
  - **Internal Tracking:** Used internally for tracking projects within the manufacturing process.
  - **Physical Labelling:** Written on the bicycle frame's steerer tube or attached to the frame as a physical identifier.
  - **Customer Reference:** Used by customers when inquiring about their project status.

The **ID** provides a straightforward, sequential identifier for physical tracking and internal references, ensuring that each project is uniquely identifiable and easily traceable throughout the manufacturing process.

#### Comparison and Usage

Both identifiers are crucial for maintaining order and clarity within the project management and manufacturing processes at Custom Bikes. While the **System ID** combines parts of the customer’s name with a random number for readability and ease of reference in digital interfaces, the **ID** is a purely numerical sequence used for physical labelling and internal tracking.

#### Sample Data

| **System ID** | **ID** | **Surname / Project** | **First Name** |
| ------------- | ------ | --------------------- | -------------- |
| THOM-R-66163  | 0915   | Thomas                | Richard        |
| RODR-M-83907  | 0916   | Rodriguez             | Mary           |

---

### Handling Duplicates in System ID and ID Columns

To ensure each project in the Custom Bikes system has a unique identifier, conditional formatting is used to catch duplicates in both the **System ID** and **ID** columns. This helps maintain the integrity of our project tracking system.

#### Conditional Formatting for Duplicates

We use the `COUNTIFS` function in Google Sheets for this purpose. The `COUNTIFS` function counts the number of cells that meet a set of criteria. In this case, it checks for duplicate values in the **System ID** and **ID** columns.

**Example Conditional Formatting Formula for System ID Column:**

```plaintext
=COUNTIFS($M$5:$M,$M5)>1
```

**Explanation:**

The conditional formatting formula `=COUNTIFS

($M$5:$M,$M5)>1` is used to identify duplicates in the **System ID** column. The range `$M$5:$M`specifies the cells to be checked, starting from row 5 in column M. The`$M5`refers to the current cell in the column that the rule is applied to, and the`>1` condition checks if the count of the value in the specified range exceeds 1, indicating a duplicate.

When a duplicate is detected, the cell is formatted with a red background (#ff0000) and yellow text (#ffff00). This visual alert signals the project owner to take action and resolve the duplication. Once the issue is resolved, the formatting returns to normal.

By using this method, any duplicates in the **System ID** or **ID** columns are immediately flagged, allowing the project owner to address the issue promptly and maintain the accuracy of the project data.

---

### System Price and Offer Price Overview

The **System Price** and **Offer Price** in the Custom Bikes system are calculated to account for various factors such as currency, VAT, configuration costs, and additional costs related to preparation, assembly, and customer communication. These prices are dynamic and adjustable, providing flexibility for both project owners and customers. The use of formulas and Google Sheets functions like `ArrayFormula`, `VLOOKUP`, and `GoogleFinance` ensures accurate and real-time calculations, supporting the efficient management of offers and pricing.

**Sample Data**

Below is a table showing the key columns related to pricing and configuration costs in the **OFFERS** sheet. This helps in understanding how the various pricing components come together:

| **CURRENCY** | **VAT** | **SYSTEM PRICE INC. VAT** | **OFFER PRICE INC. VAT** | **EST. COST OF CONFIG (PLN)** | **DESIGN COST** | **NET EST. COST OF PREPARATION AND ASSEMBLY** | **NET EST. COST OF CUSTOMER COMM.** | **NET MARGIN** | **PRICE (SYSTEM) (PLN) NET** | **"CHARM" OFFER PRICE W/ VAT** |
| ------------ | ------- | ------------------------- | ------------------------ | ----------------------------- | --------------- | --------------------------------------------- | ----------------------------------- | -------------- | ---------------------------- | ------------------------------ |
| PLN          | 23%     | 8,636                     | 8,799                    | 5,371 zł                      | 100             | 300                                           | 250                                 | 1000           | 7021.39                      | 8,649.00                       |
| EUR          | 0%      | 1,051                     | 1,079                    | 2,846 zł                      | 100             | 300                                           | 250                                 | 1000           | 4496.38                      | 1,059.00                       |

---

#### System Price (SYSTEM PRICE INC. VAT)

The **System Price** is a dynamic calculation that considers the currency, VAT rate, net estimated cost of the bike configuration (all parts and accessories), net design and painting costs, net preparation and assembly costs, net customer communication costs, and net margin. This net price is calculated in PLN (Polish złoty) before applying the VAT rate and can be converted to EUR (Euro) by using the dropdown in the currency column or preset at the time of offer creation.

**Key Components of System Price Calculation:**

1. **Currency and VAT:**

   - **Currencies:** PLN, EUR
   - **VAT Rates:** 23%, 19%, 0%
   - Conditional formatting is used to apply blue (#1155cc) text to the **CURRENCY** and **VAT** columns if EUR is the value in the **CURRENCY** column.

2. **Components of the Net System Price:**

   - **Net Estimated Cost of Configuration:** Sum of all parts, accessories, frame, and fork. Calculated by summing up the costs of all individual components required to build the bike between columns AB and BW. This includes the frame, fork, parts, and accessories.
     **Formula for Net Estimated Cost of Configuration:**
     ```plaintext
     =ArrayFormula(SUM(IFERROR(VLOOKUP(QUERY(TRANSPOSE(FILTER(AB5:BW5, AB5:BW5 <> "")),"Select * where Col1 is not null"),{IMPORT!$B$5:$B, IMPORT!$I$5:$I},2,FALSE),0)))
     ```
     This formula uses `VLOOKUP` to find the cost of each component in the range from the imported data from the inventory system and sums them up.
   - **Net Design and Painting Costs:** Sum of the design and painting cost based on the specific design, adjustable for custom jobs.
   - **Net Preparation and Assembly Costs:** Estimated costs for preparing the frame and fork, including assembling the bike.
   - **Net Customer Communication Costs:** Time spent communicating with the customer.
   - **Net Margin:** Profit margin added on top of the other costs.

3. **Formula for Net System Price:**

   ```plaintext
   =Y5+BY5+SUM(CD5:CF5)
   ```

   This formula sums the net estimated cost of configuration, net design cost, preparation and assembly cost, and net customer communication cost, and adds the net margin to calculate the net system price.

4. **Formula for System Price inc. VAT:**
   ```plaintext
   =IF(M5<>"",IFS(D5="PLN",(CG5*E5)+CG5,D5="EUR",((CG5*(index(GoogleFinance("CURRENCY:PLNEUR", "price", I5), 2, 2)))*E5)+(CG5*(index(GoogleFinance("CURRENCY:PLNEUR", "price", I5), 2, 2)))),"")
   ```

This formula uses `GoogleFinance` for currency conversion and provides real-time exchange rates, dynamically updating the **System Price** based on the selected currency and VAT rate.

---

#### Offer Price

The **Offer Price** is the final price offered to the customer, including VAT. It can be adjusted separately from the **System Price** to provide discounts or other incentives. The **Offer Price** in the Custom Bikes system is designed to be more visually appealing and "charming" compared to the **System Price**. It is generated as soon as the offer is entered into the **OFFERS** sheet and is based on the **System Price**. The **Offer Price** is rounded to create an attractive figure and is used in the official offer document provided to the customer.

**Key Features:**

- **Dependent on System Price:** The **Offer Price** is calculated based on the **System Price**.
- **Charming Rounding:** The **Offer Price** is rounded to the nearest attractive number. This value is calculated in the Charm Offer Price (PLN) column and copied to the Offer Price after offer creation if the currency dropdown is changed in the **OFFERS** sheet.
- **Editable:** The **Offer Price** can be amended by the project owner before the offer document is generated.
- **Dynamic Updates:** Changing the currency of the offer will cause the **Offer Price** to be updated accordingly.
- **Dynamic Adjustment:** The **Offer Price** can be amended at any time before the project goes live and enters production.
- **Flexibility:** Allows project owners to provide competitive pricing or adjust based on negotiations with the customer.
- **Price Safety:** To maintain profitability and ensure that the **Offer Price** is not lower than the **System Price**, conditional formatting is used to highlight the **Offer Price** column if its value is less than the **System Price**.

**Calculation of "Charm" Offer Price**

The **Charm Offer Price** is designed to create a more appealing price for the customer by rounding the **System Price** to a more attractive number. This value is calculated in the **Charm Offer Price** column and ensures that the price is visually appealing while maintaining profitability.

**Formula:**

```plaintext
=IF(D5 = "PLN", IF(MOD(F5, 100) < 50, F5 - MOD(F5, 100) + 49, F5 - MOD(F5, 100) + 99), IF(D5 = "EUR", F5 + 9 - MOD(F5, 10), "Currency Not Defined"))
```

- **For PLN:** Rounds to the nearest attractive number ending in 49 or 99.
- **For EUR:** Rounds to the nearest attractive number ending in 9.

**Example:**

- **System Price (PLN):** 8,636
  - **Charm Offer Price (PLN):** 8,649 (rounded up to the nearest attractive number ending in 49 or 99)
- **System Price (EUR):** 1,051
  - **Charm Offer Price (EUR):** 1,059 (rounded up to the nearest attractive number ending in 9)

**Implementation in Google Sheets**

**Function for Applying Charm Price:**
The charm price is applied to the **Offer Price** column using Apps Script. This process involves copying the value from the **Charm Offer Price w/ VAT** column to the **Offer Price** column.

```javascript
function getCharmPrice(row) {
  // Returns charm price configuration for the new offer row
  return {
    source: [row, OFFERS.PRICE_CHARM_PLN_EUR],
    target: [row, OFFERS.OFFER_PRICE],
  };
}

function applyCharmPrice({ source, target }, offersSheet) {
  // Copies charm price from source to target cell
  const value = offersSheet.getRange(...source).getValue();
  offersSheet.getRange(...target).setValue(value);
}
```

---

### Config and Problem Items

The **OFFERS** sheet

contains several dropdowns whose values are dynamically populated from data derived from complex custom formulas hidden from view. These results are dynamically fed into dropdown UI elements in each row to allow for easier consumption of the data. These formulas provide essential details for displaying the bike's drivetrain from the convenience of a dropdown in each project row. Additionally, they monitor the integrity of the configuration data of each project in the **OFFERS** sheet to identify and provide feedback about unrecognised items in the order.

---

#### Config Dropdown and Projects Complete Config

This formula generates a comprehensive horizontal array of all configuration items required to complete a project. It is specifically designed to display the project's drivetrain and the entire project configuration along with the quantity needed for each item. The formula processes data across hidden cells in each project row in the **OFFERS** sheet and converts this data into a dropdown item. This allows users to view the complete configuration with a single click from the **Configuration** column.

The formula filters out wheel system choices since these details are only for documentation purposes and do not have associated prices. The output includes only the essential items needed for the project's assembly.

**Example Formula:**

```plaintext
={AA5,"******",ARRAYFORMULA(TRANSPOSE(COUNTIF(QUERY(TRANSPOSE(FILTER(AB5:BW5,ISNA(MATCH(AB5:BW5,{"Tubeless ready","Tubeless","Tubes"},0)))),"Select * where Col1 is not null"),QUERY(TRANSPOSE(FILTER(AB5:BW5,ISNA(MATCH(AB5:BW5,{"Tubeless ready","Tubeless","Tubes"},0)))),"Select * where Col1 is not null"))&" x "&QUERY(TRANSPOSE(FILTER(AB5:BW5,ISNA(MATCH(AB5:BW5,{"Tubeless ready","Tubeless","Tubes"},0)))),"Select * where Col1 is not null")))}
```

**Preview Output:**

|                            |          |                              |                           |                     |                                    |                              |                       |                       |                   |                   |                             |                                               |                        |                                             |                                             |                                           |                                           |                                      |                                           |                    |                                                 |                            |          |          |                             |                                     |                               |                               |                                   |                   |
| -------------------------- | -------- | ---------------------------- | ------------------------- | ------------------- | ---------------------------------- | ---------------------------- | --------------------- | --------------------- | ----------------- | ----------------- | --------------------------- | --------------------------------------------- | ---------------------- | ------------------------------------------- | ------------------------------------------- | ----------------------------------------- | ----------------------------------------- | ------------------------------------ | ----------------------------------------- | ------------------ | ----------------------------------------------- | -------------------------- | -------- | -------- | --------------------------- | ----------------------------------- | ----------------------------- | ----------------------------- | --------------------------------- | ----------------- |
| Chain drive (Single Speed) | **\*\*** | 1 x City Man Frame 50 cm (S) | 1 x Alu. Fork (City Bike) | 1 x Neco BB 107.5mm | 1 x Shimano Alfine S501 45T, 170mm | 1 x Gates CDX Chainring, 46T | 1 x Shimano BL-4700-L | 1 x Shimano BL-4700-R | 1 x Tektro R559-F | 1 x Tektro R559-R | 1 x Gates Carbon Drive Belt | 1 x CDX Rear Sprocket, 20T for Alfine / Nexus | 1 x City Wheelset 700c | 2 x Panaracer Gravelking SS 700x38C (Black) | 2 x Panaracer Gravelking SS 700x38C (Black) | 2 x Continental Tour 28 700-32-47 FV 60mm | 2 x Continental Tour 28 700-32-47 FV 60mm | 1 x Shimano Alfine 11 (SG-S7001) 32H | 1 x Components for Alfine 8-speed SG-S700 | 1 x Cruiser (Alu.) | 1 x Brooks Slender Leather Grips, 130mm (Black) | 1 x PRO LT 100mm/31.8 +/-6 | 1 x 3 cm | 1 x 1 cm | 1 x Semi-Integrated Headset | 1 x Shimano Alfine SL-S700 11-speed | 1 x Brooks C17 Carved (Black) | 1 x Suspension Seatpost 350mm | 1 x PRO LT Seatpost Clamp, 31.8mm | 1 x Shimano GR500 |

---

### Problem Items Column and the Problem Items Formula

This formula generates a horizontal array list by first counting the total number of unknown items (parts and accessories) concerning this project and listing each of those problem items along with their quantity. This formula is crucial because unrecognised items in the offer or order data mean the pricing for these items would be missed, and tracking stock levels would be difficult. The results from the formula are fed into and transposed into the dropdown to show the complete configuration column with a single click from the **Problem Items** column.

The formula filters out wheel system choices since these details are only for documentation purposes and do not have associated prices. The output includes only the essential items needed for the project's assembly.

**Example Formula:**

```plaintext
=IF(COUNTA(IFERROR(FILTER( QUERY(FILTER(AB5:BW5,ISNA(MATCH(AB5:BW5,{"Tubeless ready","Tubeless","Tubes"},0))),"Select * where Col1 is not null"),COUNTIF(IMPORT!$B$6:$B,QUERY(FILTER(AB5:BW5,ISNA(MATCH(AB5:BW5,{"Tubeless ready","Tubeless","Tubes"},0))),"Select * where Col1 is not null"))<1)))=0,,{ COUNTA(IFERROR(FILTER( QUERY(FILTER(AB5:BW5,ISNA(MATCH(AB5:BW5,{"Tubeless ready","Tubeless","Tubes"},0))),"Select * where Col1 is not null"),COUNTIF(IMPORT!$B$6:$B,QUERY(FILTER(AB5:BW5,ISNA(MATCH(AB5:BW5,{"Tubeless ready","Tubeless","Tubes"},0))),"Select * where Col1 is not null"))<1))),"******", ArrayFormula( TRANSPOSE( COUNTIF( QUERY(TRANSPOSE(FILTER(AA5:BW5,ISNA(MATCH(AA5:BW5,{"Tubeless ready","Tubeless","Tubes"},0)))),"Select * where Col1 is not null") , FILTER( QUERY(TRANSPOSE(FILTER(AB5:BW5,ISNA(MATCH(AB5:BW5,{"Tubeless ready","Tubeless","Tubes"},0)))),"Select * where Col1 is not null"),COUNTIF(IMPORT!$B$6:$B,QUERY(TRANSPOSE(FILTER(AB5:BW5,ISNA(MATCH(AB5:BW5,{"Tubeless ready","Tubeless","Tubes"},0)))),"Select * where Col1 is not null"))<1)) &" x "& FILTER( QUERY(TRANSPOSE(FILTER(AB5:BW5,ISNA(MATCH(AB5:BW5,{"Tubeless ready","Tubeless","Tubes"},0)))),"Select * where Col1 is not null"),COUNTIF(IMPORT!$B$6:$B,QUERY(TRANSPOSE(FILTER(AB5:BW5,ISNA(MATCH(AB5:BW5,{"Tubeless ready","Tubeless","Tubes"},0)))),"Select * where Col1 is not null"))<1)) )})
```

**Example Output:**

|     |          |                                  |
| --- | -------- | -------------------------------- |
| 1   | **\*\*** | 1 x Gravel Frame (Alu) 53 cm (L) |

In this case, there is only one known part. The letter '(L)' representing the size of the frame is incorrect. The system recognised it as (M), as shown in the following entry of the **PARTS & PRICES** sheet in the Inventory spreadsheet:

| Item Name                    | Item Code        | Manufacturer | Item  | Bike Type | Filtered by | Price (PLN) (NET) |
| ---------------------------- | ---------------- | ------------ | ----- | --------- | ----------- | ----------------- |
| Gravel Frame (Alu) 53 cm (M) | FRAME.ALU.ADV.50 | Custom Bikes | Frame | Gravel    | Frame       | 1,009.00          |
